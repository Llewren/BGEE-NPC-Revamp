// Battle cry in the Wyvern Cave
IF
 InParty("CORAN")
 See("CORWYVRN")
 Global("CORAN_WYVERN_CRY","GLOBAL",0)
THEN
 RESPONSE #100
 SetGlobal("CORAN_WYVERN_CRY","GLOBAL",1)
 DisplayStringHead("CORAN",~There they are! Take the beasts down, and we'll all be in for a hefty reward.~)
END



//////////////////// TIMERED TALKS ////////////////////

// Set first friendship timer
IF
 InParty(Myself)
 Global("wn_coran_wyvern_talk","GLOBAL",2)
 Global("WNCoranChat","GLOBAL",0)
 Global("WNCoranChatActive","GLOBAL",0)
THEN
 RESPONSE #100
 RealSetGlobalTimer("WNCoranChatTimer","GLOBAL",1800)
 SetGlobal("WNCoranChat","GLOBAL",1)
 SetGlobal("WNCoranChatActive","GLOBAL",1)
END

//(a) increment the global variable when the timerâ€™s countdown is finished
IF
	InParty(Myself)
	CombatCounter(0) 
	See(Player1) 
	!StateCheck("CORAN",CD_STATE_NOTVALID) 
	!StateCheck(Player1,CD_STATE_NOTVALID) 
	RealGlobalTimerExpired("WNCoranChatTimer","GLOBAL") 
	Global("WNCoranChatActive","GLOBAL",1) 
	OR(2) 
		Global("WNCoranChat","GLOBAL",1)
		Global("WNCoranChat","GLOBAL",4)
	THEN
		RESPONSE #100
		IncrementGlobal("WNCoranChat","GLOBAL",1) 
END

//(b) make the character talk when the variable is right
IF
	OR(2)
		Global("WNCoranChat","GLOBAL",2)
		Global("WNCoranChat","GLOBAL",5)
	THEN
		RESPONSE #100
		MoveToObject(Player1)
		StartDialogue("CORANJ",Player1)
END


// (c) increment global and set the timer after the fired talk.
IF
	InParty(Myself) 
	Global("WNCoranChatActive","GLOBAL",1)
	OR(2)
		Global("WNCoranChat","GLOBAL",3)
		Global("WNCoranChat","GLOBAL",6)
	THEN
	RESPONSE #100
		IncrementGlobal("WNCoranChat","GLOBAL",1)
		RealSetGlobalTimer("WNCoranChatTimer","GLOBAL",1800)
END


//////////////////// SCENERY TALKS ////////////////////

// QUEST 1: AFTER TURNING IN THE WYVERN HEAD

IF
	InParty(Myself)
	CombatCounter(0)
	!See([ENEMY])
	See(Player1)
	Global("CoranReward","GLOBAL",1)
	Global("wyvernp","GLOBAL",1)
	Global("wn_coran_wyvern_talk","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("wn_coran_wyvern_talk","GLOBAL",1)
		SetGlobalTimer("wn_coran_wyvern_talk_timer","GLOBAL",ONE_ROUND)
END

IF
	Global("wn_coran_wyvern_talk","GLOBAL",1)
	GlobalTimerExpired("wn_coran_wyvern_talk_timer","GLOBAL")
THEN
	RESPONSE #100
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END

// QUEST 2: BRIELBARA MET THE PARTY

IF
	InParty(Myself)
	CombatCounter(0)
	!See([ENEMY])
	See(Player1)
	Global("bd_briel_coran","GLOBAL",1)
	Global("wn_brielcoran_talk","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("wn_brielcoran_talk","GLOBAL",1)
		SetGlobalTimer("wn_brielcoran_talk_timer","GLOBAL",2)
END

IF
	Global("wn_brielcoran_talk","GLOBAL",1)
	GlobalTimerExpired("wn_brielcoran_talk_timer","GLOBAL")
THEN
	RESPONSE #100
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END

// QUEST 3: BRIELBARA'S BABY COMPLETE

IF
	InParty(Myself)
	CombatCounter(0)
	!See([ENEMY])
	See(Player1)
	Global("bd_briel_coran","GLOBAL",1)
	Global("BrielbaraMove","GLOBAL",2)
	Global("wn_coranquest_end","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("wn_coranquest_end","GLOBAL",1)
		SetGlobalTimer("wn_coranquest_end_timer","GLOBAL",ONE_ROUND)
END

IF
	Global("wn_coranquest_end","GLOBAL",1)
	GlobalTimerExpired("wn_coranquest_end_timer","GLOBAL")
THEN
	RESPONSE #100
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END

