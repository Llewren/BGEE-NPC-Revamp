// Set first friendship timer
IF
 InParty(Myself)
 Global("WNAjantisChat","GLOBAL",0)
 Global("WNAjantisChatActive","GLOBAL",0)
THEN
 RESPONSE #100
 RealSetGlobalTimer("WNAjantisChatTimer","GLOBAL",1800)
 SetGlobal("WNAjantisChat","GLOBAL",1)
 SetGlobal("WNAjantisChatActive","GLOBAL",1)
END


//////////////////// FRIENDSHIP TALKS ////////////////////

//(a) increment the global variable when the timerâ€™s countdown is finished
IF
	InParty(Myself)
	CombatCounter(0) 
	See(Player1) 
	!StateCheck("AJANTIS",CD_STATE_NOTVALID) 
	!StateCheck(Player1,CD_STATE_NOTVALID) 
	RealGlobalTimerExpired("WNAjantisChatTimer","GLOBAL") 
	Global("WNAjantisChatActive","GLOBAL",1) 
	OR(2) 
		Global("WNAjantisChat","GLOBAL",1)
		Global("WNAjantisChat","GLOBAL",4)
	THEN
		RESPONSE #100
		IncrementGlobal("WNAjantisChat","GLOBAL",1) 
END

//(b) make the character talk when the variable is right
IF
	OR(2)
		Global("WNAjantisChat","GLOBAL",2)
		Global("WNAjantisChat","GLOBAL",5)
	THEN
		RESPONSE #100
		MoveToObject(Player1)
		StartDialogue("AJANTJ",Player1)
END
 

// (c) increment global and set the timer after the fired talk.
IF
	InParty(Myself) 
	Global("WNAjantisChatActive","GLOBAL",1)
	OR(2)
		Global("WNAjantisChat","GLOBAL",3)
		Global("WNAjantisChat","GLOBAL",6)
	THEN
	RESPONSE #100
		IncrementGlobal("WNAjantisChat","GLOBAL",1)
		RealSetGlobalTimer("WNAjantisChatTimer","GLOBAL",1800)
END

// OTR Conversation 3

IF
	InParty(Myself)
	CombatCounter(0)
	!See([ENEMY])
	See(Player1)
	GlobalGT("Chapter","GLOBAL",3)
	Global("WNAjantisChat","GLOBAL",7)
THEN
	RESPONSE #100
		SetGlobal("WNAjantOTR4","GLOBAL",1)
		SetGlobalTimer("WNAjantOTR4Timer","GLOBAL",1800)
END

IF
	Global("WNAjantOTR4","GLOBAL",1)
	GlobalTimerExpired("WNAjantOTR4Timer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("WNAjantisChat","GLOBAL",8)
		SetGlobal("WNAjantOTR4","GLOBAL",2)
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END





//////////////////// QUEST TALKS ////////////////////


// QUEST 1: TEMPLE OF HELM IN THIS AREA

IF
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
See(Player1)
AreaCheck("AR0200")
Global("WNAjantisQuest","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("WNAjantisQuest","GLOBAL",1)
END

IF
Global("WNAjantisQuest","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogueNoSet(Player1)
END

// QUEST 2: PEITOR IS NOT HERE

IF
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
See(Player1)
AreaCheck("AR0115")
OR(2)
		!TimeOfDay(DUSK) 
		!TimeOfDay(NIGHT) 
Global("WNAjantisTimeTalk","GLOBAL",0)
Global("WNAjantisQuest","GLOBAL",10)
THEN
RESPONSE #100
SetGlobal("WNAjantisTimeTalk","GLOBAL",1)
END

IF
Global("WNAjantisTimeTalk","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogueNoSet(Player1)
END

// QUEST 3: THE QUEST IS OVER

IF
	InParty(Myself)
	CombatCounter(0)
	!See([ENEMY])
	See(Player1)
	Global("WNAjantisQuestDone","GLOBAL",1)
	Global("WNAjantisQuestDoneTalk","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("WNAjantisQuestDoneTalk","GLOBAL",1)
		SetGlobalTimer("WNAjantisQuestDoneTalkTimer","GLOBAL",3)
END

IF
	Global("WNAjantisQuestDoneTalk","GLOBAL",1)
	GlobalTimerExpired("WNAjantisQuestDoneTalkTimer","GLOBAL")
THEN
	RESPONSE #100
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END